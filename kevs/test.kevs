// create the two platform nodes
add javaNode : JavaNode
add jsNode : JavascriptNode

// provide a network way to connect to those nodes
network javaNode.ip.lo 127.0.0.1
network jsNode.ip.lo 127.0.0.1

// create a group to share the model between the nodes
add group : WSGroup
// set the master node for the group to be the jsNode
set group.master = 'jsNode'
// attach the nodes to the group
attach javaNode, jsNode group

// add the Ticker on the Java platform
add javaNode.ticker : Ticker/5.2.2

// add the ConsolePrinter and the WSMsgBroker on the JS platform
add jsNode.printer : ConsolePrinter/5.2.2
add jsNode.broker : WSMsgBroker

// define on which port the broker will start
set jsNode.broker.port = '9050'

// add a WSChan to transmit the messages between the Ticker and the ConsolePrinter
add chan : WSChan

// give the host:port of the broker to the chan
set chan.host = "localhost"
set chan.port = "9050"

// bind the ticker to the chan
bind javaNode.ticker.tick chan

// bind the printer to the chan
bind jsNode.printer.input chan